debuggr = require \_debugger
async = require \async
{ok: assert} = require \assert

client = new debuggr.Client
client.connect 5858, \127.0.0.1

client.setProperty = (handle, property, value, cb) ->
  command =
    command: \evaluate
    arguments:
      expression: "$$handle$$[#{JSON.stringify property}] = #{JSON.stringify value}"
      global: true
      disable_break: true
      additional_context: [name: \$$handle$$, handle: handle]
  client.req command, cb

client.lookup = (handles, cb) ->
  result = []
  <- async.forEach handles, (handle, cb) ->
    (err, refsData) <- client.req command: \lookup, arguments: handles: [handle], includeSource: true
    # FIXME ignores errors!
    if not err
      result.push refsData[(Object.keys refsData)[0]]
    else
      console.log "warning: lookup error for #{handle}"
    cb!
  cb result

extractRefs = (obj, refsarr) ->
  return if typeof obj is not \object or obj is null
  refsarr or= []
  # console.log "traversing..."
  if typeof obj.ref is \number
    refsarr.push obj.ref
    # console.log "met #{obj.ref}"
  for key, value in obj
    extractRefs value, refsarr
  refsarr

arrSubtract = (array, blacklist) ->
  for value of array
    value if value not of blacklist

arrUniq = (array) ->
  result = []
  for value of array
    if value not of result
      result.push value
  result

unserializify = (obj, refs, handled) ->
  return obj if typeof obj is not \object or obj is null
  refs ||= obj.refs
  handled ||= []
  if obj.ref? and typeof obj.ref is \number
    obj.ref = refs[obj.ref]
  for key, value in obj
    if handled.indexOf(value) is -1
      handled.push value
      unserializify value, refs, handled
  delete obj.refs if obj.refs is refs
  obj

concatLines = (array, indent = 0) ->
  lines = for text of array
    for line of text.split '\n'
      line
  spaces = (' ' for n til indent).join ''
  lines.map (line) ->
    spaces + line
  .join '\n'

makeSupertrace = (btbody) ->
  concatLines (for frame of btbody.frames
    scopes = concatLines (for {object: {ref: scope}}, i of frame.scopes.slice 0, -2
      locals = concatLines (for {name, ref: value} of scope.properties
        if value?
          if value.value?
            "#{JSON.stringify name}: #{JSON.stringify value.value}"
          else
            moreData = if value.type is \function
              " #{value.name or value.inferredName} at #{value.script?.name || '<unknown>'}:#{value.line}:#{value.column}"
            else if value.properties?
              keys = (property.name for property of value.properties)
              " with keys #{keys.join ', '}"
            else ""
            "#{JSON.stringify name}: #{value.className}#{moreData}"
      ), 2
      concatLines ["scope #{i}", locals]
    ), 2
    concatLines [frame.text, scopes]
  ), 4

client.on \unhandledResponse, (body) ->
  if not (body.type is \event and body.event is \exception)
    console.log "##### INVALID MESSAGE: #{JSON.stringify body}"
    return
  console.log "breakpoint hit"
  console.log "fetching a stacktrace..."
  (err, btbody) <- client.req command: \backtrace
  throw err if err
  console.log "fetching scopes..."
  assert (Array.isArray btbody.frames), "frames is a #{typeof btbody.frames}, btbody keys: #{(Object.keys btbody).join!}"
  <- async.forEach btbody.frames, (frame, done) ->
    assert (Array.isArray frame.scopes), 'scopes is not an array'
    (_, frame.scopes) <- async.map frame.scopes, ({index: scopeNumber}, done) ->
      console.log "requesting scope #{scopeNumber}:#{frame.index}"
      (err, scope) <- client.req command: \scope, arguments: number: scopeNumber, frameNumber: frame.index
      throw err if err
      done null, scope
    done!
  console.log "fetching refs..."
  btbody.refs = {}
  fetchedRefs = []
  unfetchedRefs = extractRefs btbody
  <- fetchMoreRefs = (cb) ->
    console.log "fetching #{unfetchedRefs.join! || \none}"
    (refsData) <- client.lookup unfetchedRefs
    unfetchedRefs := []
    for key, handleData in refsData
      fetchedRefs.push handleData.handle
      btbody.refs[handleData.handle] = handleData
      unfetchedRefs := unfetchedRefs.concat extractRefs handleData
    unfetchedRefs := arrUniq arrSubtract unfetchedRefs, fetchedRefs
    if unfetchedRefs.length > 0
      fetchMoreRefs cb
    else
      cb!
  console.log "attaching data..."
  realBtbody = unserializify JSON.parse JSON.stringify btbody
  supertrace = makeSupertrace realBtbody
  (err) <- client.setProperty body.body.exception.handle, \stack, supertrace
  throw err if err
  console.log "resuming..."
  (err, body, res) <- client.reqContinue
  throw err if err
  console.log "resumed"

client.on \ready, ->
  (err, body, res) <- client.req command: \setexceptionbreak, arguments: type: \all, enabled: true
  throw err if err
  console.log "exception breakpoint set: #{JSON.stringify body}"
  (err) <- client.req command: \continue
  throw err if err
  console.log "started"
